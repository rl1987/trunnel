
=================== HERE IS OUR ACTUAL GRAMMAR, extracted from Grammar.py

         File ::= Declaration
         File ::= File Declaration

         Declaration ::= OptAnnotation ConstDecl
         Declaration ::= OptAnnotation StructDecl

         ConstDecl ::= const CONST_ID = INT ;

         StructDecl ::= struct ID { StructMembers StructEnding }
         StructEnding ::=
         StructEnding ::= eos ;
         StructEnding ::= SMRemainder ;

         SMRemainder ::= OptAnnotation u8 ID [ ]

         StructMembers ::= OptAnnotation StructMember ;
         StructMembers ::= StructMembers OptAnnotation StructMember ;

         Integer ::= INT
         Integer ::= CONST_ID

         OptAnnotation ::=
         OptAnnotation ::= ANNOTATION

         StructMember ::= SMInteger
         StructMember ::= SMStruct
         StructMember ::= SMString
         StructMember ::= SMArray
         StructMember ::= SMUnion

         SMInteger ::= IntType ID OptIntConstraint
         IntType ::= u8
         IntType ::= u16
         IntType ::= u32
         IntType ::= u64

         OptIntConstraint ::=
         OptIntConstraint ::= IN [ IntList ]

         IntList ::= IntListMember
         IntList ::= IntList , IntListMember
         IntListMember ::= Integer
         IntListMember ::= Integer .. Integer

         SMStruct ::= struct ID ID
         SMString ::= nulterm ID
         SMArray ::= SMFixedArray
         SMArray ::= SMVarArray
         ArrayBase ::= IntType
         ArrayBase ::= struct ID
         ArrayBase ::= char
         SMVarArray ::= ArrayBase ID [ ID ]
         SMFixedArray ::= ArrayBase ID [ Integer ]

         SMUnion ::= union ID [ ID ] OptUnionLength { UnionMembers }
         OptUnionLength ::=
         OptUnionLength ::= WITH LENGTH ID

         UnionMembers ::= UnionMember
         UnionMembers ::= UnionMembers UnionMember
         UnionMember ::= UnionCase : UnionFields OptExtentSpec
         UnionCase ::= IntList
         UnionCase ::= default
         OptExtentSpec ::=
         OptExtentSpec ::= ... ;
         OptExtentSpec ::= SMRemainder ;
         UnionFields ::= ;
         UnionFields ::= UnionField ;
         UnionFields ::= UnionFields UnionField ;
         UnionFields ::= fail ;
         UnionFields ::= ignore ;
         UnionFields ::= SMRemainder ;
         UnionField ::= SMInteger
         UnionField ::= SMFixedArray
         UnionField ::= SMString
         UnionField ::= SMStruct


Additional constraints:

   Structure declarations form a DAG.

   Field references in SMVarArray and SMUnion and SMUnionLength refer
   only to earlier-occurring fields in the same structure.

   No ExtentSpec unless the union has a UnionLength.

